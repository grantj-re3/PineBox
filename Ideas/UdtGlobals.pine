//@version=5
indicator("Virtual-globals using UDTs", shorttitle = "UdtGlobals")

//////////////////////////////////////////////////////////////////////////////
// Declare globals for functions
type globalUdt
    float sumOpen  = 0.0
    float sumClose = 0.0
    int numBars = 0
    int numUp   = 0

var global = globalUdt.new()

//////////////////////////////////////////////////////////////////////////////
// Functions
gatherStats() =>
    global.sumOpen += open
    global.sumClose += close

    global.numBars += 1
    global.numUp += close > open ? 1 : 0

showStats() =>
    if bar_index < 50 or bar_index % 100 == 0
        log.info("{0,number,0} | {1,number,0.00} | {2}: {3,number,0.00} | {4}: {5,number,0} | %OfBarsUp: {6,number,percent}", 
          bar_index, open,  "--",  global.sumOpen,
          "--", global.numUp,
          1.0 * global.numUp / global.numBars)

        log.info("{0,number,0} | {1,number,0.00} | {2}: {3,number,0.00} | {4}: {5,number,0} | Avg: {6,number,0.00}", 
          bar_index, close, "--", global.sumClose,
          "--", global.numBars,
          global.sumClose / global.numBars)

//////////////////////////////////////////////////////////////////////////////
// Main
gatherStats()
showStats()

