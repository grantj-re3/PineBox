//@version=5
indicator("Virtual-globals using maps", shorttitle = "MapGlobals")

//////////////////////////////////////////////////////////////////////////////
// Constants
KEY_SUM_OPEN  = "sumOpen"   // Key for: Sum of open prices
KEY_SUM_CLOSE = "sumClose"  // Key for: Sum of close prices

KEY_NUM_BARS  = "numBars"   // Key for: Number of bars
KEY_NUM_UP    = "numUp"     // Key for: Number of bars where close > open

//////////////////////////////////////////////////////////////////////////////
// Declare globals for functions
var globalFloats = map.new<string, float>()
var globalInts   = map.new<string, int>()

//////////////////////////////////////////////////////////////////////////////
// Functions
initStats() =>
    for key in array.from(KEY_SUM_OPEN, KEY_SUM_CLOSE)
        globalFloats.put(key, 0.0)

    for key in array.from(KEY_NUM_BARS, KEY_NUM_UP)
        globalInts.put(key, 0)

gatherStats() =>
    globalFloats.put(KEY_SUM_OPEN,  globalFloats.get(KEY_SUM_OPEN) + open)
    globalFloats.put(KEY_SUM_CLOSE, globalFloats.get(KEY_SUM_CLOSE) + close)

    globalInts.put(KEY_NUM_BARS, globalInts.get(KEY_NUM_BARS) + 1)
    globalInts.put(KEY_NUM_UP,   globalInts.get(KEY_NUM_UP) + (close > open ? 1 : 0))

showStats() =>
    if bar_index < 50 or bar_index % 100 == 0
        log.info("{0,number,0} | {1,number,0.00} | {2}: {3,number,0.00} | {4}: {5,number,0} | %OfBarsUp: {6,number,percent}", 
          bar_index, open,  KEY_SUM_OPEN,  globalFloats.get(KEY_SUM_OPEN),
          KEY_NUM_UP, globalInts.get(KEY_NUM_UP),
          1.0 * globalInts.get(KEY_NUM_UP) / globalInts.get(KEY_NUM_BARS))

        log.info("{0,number,0} | {1,number,0.00} | {2}: {3,number,0.00} | {4}: {5,number,0} | Avg: {6,number,0.00}", 
          bar_index, close, KEY_SUM_CLOSE, globalFloats.get(KEY_SUM_CLOSE),
          KEY_NUM_BARS, globalInts.get(KEY_NUM_BARS),
          globalFloats.get(KEY_SUM_CLOSE) / globalInts.get(KEY_NUM_BARS))

//////////////////////////////////////////////////////////////////////////////
// Main
if bar_index == 0
    initStats()

gatherStats()
showStats()

